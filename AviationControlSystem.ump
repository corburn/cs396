// All units of measurement assume International Civil Aviation Organization standards

namespace aviationcontrolsystem;

class AviationControlSystem {

    public static void main(String[] args) {
        System.out.println("Initializing Aviation Control System Simulation");
        AviationControlSystem acs = new AviationControlSystem();
        acs.addAirport(new AirTrafficControl());
        acs.addAirport(new AirTrafficControl());

        Plane plane = new Plane();
        plane.preFlightCheck();
        plane.addFlightPlan(plane.addFlightPlan(acs.getAirport(0), acs.getAirport(1)));
        plane.preFlightCheck();
        plane.holdingPattern();

        /*AviationControlSystem acs = new AviationControlSystem();

          AirTrafficControl here = new AirTrafficControl(50, "Here Name", "Here Place", new Pipeline(), new Pipeline());
          AirTrafficControl there = new AirTrafficControl(50, "There Name", "There Place", new Pipeline(), new Pipeline());
          acs.addAirport(here.getAirport());
          acs.addAirport(there.getAirport());

          Plane thePlane = new Plane("Risk", 666, "Mercury", "Firestorm", "CX7", 20000);
          Captain jackHarkness = new Captain();
          java.sql.Date now = new java.sql.Date(new Date().getTime());
          FlightPlan thePlan = new FlightPlan(jackHarkness, now, now, 95.0, thePlane, here.getAirport(), there.getAirport(), hereWeAre);
          thePlane.preFlightCheck();

          Terminal hereWeAre = new Terminal(here.getAirport());
          Terminal thereWeGo = new Terminal(there.getAirport());

          Passenger uno = new Passenger("Priest", 56, hereWeAre, thePlan);
          Passenger dos = new Passenger("Rabbi", 30, hereWeAre, thePlan);
          Passenger tres = new Passenger("Minister", 42, hereWeAre, thePlan);
          Passenger cuatro = new Passenger("StewieGriffin", 1, hereWeAre, thePlan);
          */
    }
    1 -- * Airport;
}

//class Airline {
//    0..1 -- 1..* Plane;
//    name;
//}

class Airport {
    1 -- 1..* Runway;
    //immutable name;
    //immutable address;
    // altitude in feet
    //immutable int altitude;
    // TODO close airport
    lazy boolean isEmergency;
}

class AirTrafficControl {
    // AirTrafficControl may manage a region if an Airport does not have a control tower or
    // several Airports are located in close proximity
    1 -- * Airport;
    Plane[] takeoffPipeline;
    Plane[] landingPipeline;
    // TODO
    // Aircraft should be able to request hazards information such as weather
    // (rain, ice, fog (range of visibility), wind gusts (direction, speed, etc)

    // Operating range in Nautical Miles
    //immutable int range;
    sm {
        Monitor {
            do {
                // TODO
                // TODO check for runway availability and runway / airport closure
                if(hasLandingPipeline()) {
                    getLandingPipeline(0).grantClearance();
                } else if(hasTakeoffPipeline()) {
                    getTakeoffPipeline(0).grantClearance();
                }
                // Cannot directly use a loop because Java will complain about unreachable
                // code Thread.sleep(1)
                loop();
            }
            // Must have a state transition otherwise umple will generate invalid code.
            // It will call an undefined exitMonitor() method.
            loop -> Monitor;
        }
    }
}

class Baggage {
}

class Captain {
    isA FlightDeckCrew;
    //name;
    //address;
    //phoneNumber;
    //homeBase;
}

// A FlightPlan must include aircraft type and special equipment
// The plane object should define an array of equipment on board
// http://en.wikipedia.org/wiki/Equipment_codes
//class Equipment {
//}

// CRITICISM:
// It is assumed
// - FlightPlans are sorted by date
// - FlightPlans are valid
// Based on (FAA) Domestic Flight Plan Form 7233-1.
// registration, color, aircraft type, special equipment, and fuelOnBoard
// can be obtained from the Plane object
class FlightPlan {
    // departure, destination, and alternate airports in the event the destination is unavailable
    * -- 2..* Airport;
    * -> * CabinCrew;
    * -> * FlightDeckCrew;
    * -> * Passenger;
    * -- 1 Plane;
    // Type of flight: VFR, IFR, DVFR, or a composite thereof
    //typeFlight;
    // Cruising speed in knots
    //int airspeed;
    //Date proposedDepartureTime;
    //lazy Date actualDepartureTime;
    // Cruising altitude as a Flight Level
    //int flightLevel
    //Proposed route of flight. The route can be made up of airways, intersections, navaids, or possibly direct.
    //Route[] route;
    // Estimated flight time in minutes
    //int estTimeEnRoute;
    // Additional information for AirTrafficControl
    //remarks;
    // At least one of the FlightDeckCrew must be a Captain
    //Captain captain
}

class Passenger {
    1 -- * Baggage;
    //name;
    //int age;
}

class Plane {
    // Aircraft Registration
    //immutable registration;
    // TODO: divide into business class/coach class and amenities
    //immutable int seats;
    //immutable int maxWeight;
    //immutable make;
    //immutable model;
    //immutable series;
    // Color to help search and rescue personnel identify the aircraft
    //immutable color;
    // Type of aircraft and special equipment
    // For example, a Mitsubishi Mu-2 equipped with an altitude reporting transponder and GPS would use MU2/G
    //immutable typeAircraft
    // Unscheduled general aviation will generally use registration
    // Commercial operators will use ICAO airline designator and route number
    //flightCode = registration;
    // Defaults to flight code but may be reassigned by Air Traffic Control
    //defaulted callsign = flightCode;
    // TODO fuel min / max guards
    // Cannot have negative fuel or exceed capacity
    // TODO define fuel capacity
    // Amount of fuel on board the aircraft in flight minute units
    lazy int fuelOnBoard;
    lazy boolean isEmergency;
    // Weather deposits ice on the control surfaces
    lazy boolean hasIce;
    // AirTrafficControl can instruct the Plane to fly at a given altitude
    //lazy int altitude;
    // A crude function to print the entry, action, and exit state transitions
    private void printTransition(String event, String state) {
        //System.out.println("Flight " + getCallsign() + " " + event + " " + state);
        System.out.println(toString() + " " + event + " " + state);
    }

    // TODO it should request AirTrafficControl add the plane to the appropriate pipeline
    public void requestClearance() {
        // takeoff pipeline
        this.getFlightPlan(0).getAirport(0).getAirTrafficControl().addTakeoffPipeline(this);
        // TODO landing pipeline
    }
    // AirTrafficControl can remove the plane from the pipeline
    public void acknowledgeClearance() {
        this.getFlightPlan(0).getAirport(0).getAirTrafficControl().removeTakeoffPipeline(this);
        // TODO landing pipeline
    }
    private boolean hasPassengersAtTerminal() {
        //return getFlightPlan(0).getTerminal(0).hasPassengers();
        return false;
    }
    // Transfer Passengers from Terminal to Plane
    private void board() {
        /*FlightPlan plan = getFlightPlan(0);
        Terminal terminal = plan.getTerminal(0);
        List<Passenger> passengers = terminal.getPassengers();
        for(Passenger passenger : passengers) {
            plan.addPassenger(passenger);
            terminal.removePassenger(passenger);
        }*/
    }
    // Transfer Passengers from Plane to Terminal
    private void disembark(int terminal) {
        /*Terminal awesomeTerminal = getFlightPlan(0).getTerminal(terminal);
          FlightPlan awesomePlan = getFlightPlan(0);
          List<Passenger> morePassengers = awesomePlan.getPassengers();
          for (Passenger passenger : morePassengers) {
          awesomeTerminal.addPassenger(passenger);
          awesomePlan.removePassenger(passenger);
          }*/
    }
    // TODO handle emergencies (flight, passenger, etc)
    // Add declareEmergency() function and appropriate transition to all states.
    // Add [isEmergency] guarded auto transition to all state to handle emergency
    // declared in any earlier state.
    sm {
        flight {
            Standby {
                entry / {
                    printTransition("entering", "Standby");
                }
                preFlightCheck() [!hasFlightPlans()] / { System.out.println(this.toString() + " cannot proceed hasFlightPlans: " + this.hasFlightPlans()); } -> Standby;
                preFlightCheck() [hasPassengersAtTerminal()] -> Board;
                preFlightCheck() [!hasPassengersAtTerminal()] -> TakeoffPipeline;
            }
            Board {
                entry / {
                    printTransition("entering", "Board");
                }
                do {
                    board();
                    done();
                }
                done() -> TakeoffPipeline;
            }
            TakeoffPipeline {
                entry / {
                    printTransition("entering", "TakeoffPipeline");
                    requestClearance();
                }
                Ready {
                    entry / {
                        printTransition("entering", "Ready");
                    }
                    [hasIce] -> DeIce;
                }
                DeIce {
                    entry / {
                        printTransition("entering", "DeIce");
                    }
                    // Aircraft must takeoff within the deicing fluid holdover or else wait
                    // to be deiced again
                    //
                    // TODO: the holdover period should be a constant defined by the
                    // AviationControlSystem. Magic numbers buried in the code are bad practice.
                    // 15 minutes
                    after(15*60) [hasIce] -> DeIce;
                }
                // TODO replace with AirTrafficControl.acknowledgeClearance(Plane);
                // 'rotate' is jargon for when an aircraft lifts up from the runway
                grantClearance() / {
                    printTransition("transition", "TakeoffPipeline -> Transit");
                } -> Takeoff;
            }
            Takeoff {
                // TODO: the expiration period should be a constant defined by AirTrafficControl
                // or the AviationControlSystem
                // After 5 minutes permission expires
                // Must have a guard condition otherwise umple ignores other transitions
                after(5*60) [true] -> TakeoffPipeline;
                takeoff() / { acknowledgeClearance(); } -> Transit;
            }
            Transit {
                entry / {
                    printTransition("entering", "Transit");
                }
                // TODO
                // Add transitions between AirTrafficControllers en route as the aircraft
                // enters/leaves their airspace
                holdingPattern() -> LandingPipeline;
            }
            LandingPipeline {
                entry / {
                    printTransition("entering", "LandingPipeline");
                    requestClearance();
                }
                grantClearance() -> Land;
            }
            Land {
                // After 5 minutes permission expires
                // Must have a guard condition otherwise umple ignores other transitions
                after(5*60) [true] -> LandingPipeline;
                land() / { acknowledgeClearance(); } -> Disembark;
            }
            // TODO handle Missing In Action / Crash states
            Disembark {
                entry / {
                    // Any landing you walk away from is a good landing
                    printTransition("entering", "Disembark");
                }
                -> Standby;
                exit / {
                    // Remove the completed FlightPlan from the queue
                    // TODO: use the sorted directive to make sure flights are sorted by Date
                    removeFlightPlan(getFlightPlan(0));                
                }
            }
            emergency() -> Abort;
        }
        Abort {
            entry / {
                // Any landing you walk away from is a good landing
                printTransition("entering", "Abort");
            }
        }
    }
}

class Runway {
    * -> * GroundCrew;
    int capacity;
    //int id;
    boolean isLatitudinal;
    runwayState2 {

        Open {
            crisis -> EmergencyShutdown;

            Available {
                planeDeparting -> InUse;
                planeJustLanded -> InUse;
                repair -> Reconstruction;
                weather -> Reconstruction;
            }

            InUse {
                planeDeparted -> Available;
                planeReachesTerminal -> Available;
            }
        }

        Closed {
            EmergencyShutdown {
                crisisResolved -> RunwaySafe;
            }

            RunwaySafe {
                opening -> Open;
            }

            Reconstruction {
                Repair {
                    completed -> Inspect;
                }
                Inspect { 
                    upToStandard -> RunwaySafe;
                    notUpToStandard -> Repair; 
                }
            }
        } 
    }
    direction {
        Undefined {
            [isLatitudinal] -> North;
            [!isLatitudinal] -> East;
        }
        North {
            changeDirection() -> South;
        }
        South {
            changeDirection() -> North;
        }
        East {
            changeDirection() -> West;
        }
        West {
            changeDirection() -> East;
        }
    }
}

class Terminal {
    * -- 1 Airport;
    * -> * Passenger;
}

// Loadmaster, crew member on cargo aircraft responsible for loading freight and personnel and the weight and balance of the aircraft.
// Purser or Customer Service Manager is responsible for the cabin crew as a team leader.
// Flight attendant or Cabin Crew, crew member responsible for the safety of passengers.
interface CabinCrew {
    //isA FlightCrew;
}

// Captain, the pilot designated as the Pilot in command[1]
// Co-pilot, another pilot who is not the pilot in command,[1] normally called the First Officer.
// First officer, a pilot who is not the pilot in command.
// Second officer
// Third officer
// Flight engineer, on older aircraft they were the crew member responsible for engines, systems and fuel management.
interface FlightDeckCrew {
    //isA FlightCrew;
}

// Regulations limit work hours and minimum rest hours
//interface FlightCrew {
//}

// maintenance, fueling, luggage, etc
interface GroundCrew {
}
