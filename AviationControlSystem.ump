// All units of measurement assume International Civil Aviation Organization standards

namespace aviationcontrolsystem;

class AviationControlSystem {
    public static void main(String[] args) {
        System.out.println("Initializing Aviation Control System Simulation");
        //AviationControlSystem acs = new AviationControlSystem();
                
        //acs.addAirport(new Airport("Here Name", "Here Place", 5, acs));
        //acs.addAirport(new Airport("There Name", "There Place", 5, acs));
        
        // TODO
        // - add pipelines to AirTrafficControl
        // - Plane adds itself to the takeoff pipeline
        // - AirTrafficControl state machine sees queue is not empty
        // - ATC checks all clear and grants clearance
        // - Plane takes off removing itself from the pipeline and reseting clearance
        // TODO
        // Figure out how/when ATC can reset clearance

        // The after() directives launch threads that continue to run after main has finished
        //System.out.println("End Simulation");
        //System.exit(0);
    }
    1 -- * Airport;
}

class Captain {
    isA FlightDeckCrew;
}

// Captain, the pilot designated as the Pilot in command[1]
// Co-pilot, another pilot who is not the pilot in command,[1] normally called the First Officer.
// First officer, a pilot who is not the pilot in command.
// Second officer
// Third officer
// Flight engineer, on older aircraft they were the crew member responsible for engines, systems and fuel management.
interface FlightDeckCrew {
    //isA FlightCrew;
}

// Loadmaster, crew member on cargo aircraft responsible for loading freight and personnel and the weight and balance of the aircraft.
// Purser or Customer Service Manager is responsible for the cabin crew as a team leader.
// Flight attendant or Cabin Crew, crew member responsible for the safety of passengers.
interface CabinCrew {
    //isA FlightCrew;
}

// Regulations limit work hours and minimum rest hours
//interface FlightCrew {
//}

// maintenance, fueling, luggage, etc
interface GroundCrew {
}

class Airline {
    0..1 -- 1..* Plane;
}

class Airport {
    1 -- 1 AirTrafficControl;
    1 -- 1..* Runway;
    immutable name;
    immutable address;
    // altitude in feet
    //immutable int altitude;
    // TODO close airport
    lazy boolean isEmergency;
}

// Criticism: the assignment specification requires every airport have a control unit,
// however, not all airports will have a control unit in which case a unit would have
// authority over a region.
class AirTrafficControl {
    // TODO
    // Aircraft should be able to request hazards information such as weather
    // (rain, ice, fog (range of visibility), wind gusts (direction, speed, etc)
    
    // TODO timeout for when a plane is kicked out of a pipeline should be defined here
    //int timeout;
    // Operating range in Nautical Miles
    int range;
    // TODO
    // To enter a pipeline state, the plane calls this method to add itself to the queue.
    // In the below state machine, the request is granted when the plane is assigned to a runway
    public void requestClearance(Plane plane) {
    }
    // TODO
    // The plane calls this method after/during takeoff to indicate the runway is available again
    public void acknowledgeClearance(Plane plane) {
        // TODO
    }
    queued sm {
        // TODO
        // The intended implementation is that it should idle in the MonitorAirspace state
        // with a [hasPlanesInPipeline] Guard condition to a pipeline where a single request
        // is handled before returning to the MonitorAirspace state.
        // while(hasPlanesInPipeline) {
        //    handleRequest()
        // }
        MonitorAirspace {
        }
        TakeoffPipeline {
            -> MonitorAirspace;
        }
        LandingPipeline {
            -> MonitorAirspace;
        }
    }
}

class Baggage {
}

class FlightPlan {
    // origin and destination
    * -- 2 Airport;
    * -> * CabinCrew;
    * -> * FlightDeckCrew;
    // At least one of the FlightDeckCrew must be a Captain
    Captain captain;
    // Discovered UMPLE imports java.sql.Date when trying to define
    // depend java.util.Date
    // Departure and arrival times
    Date start;
    Date end;
    // Minimum gallons of fuel required to complete the flight
    // TODO implement guard conditions to prevent flight with insufficient fuel
    double minFuel;
}

class Passenger {
    * -- 1..* FlightPlan;
    1 -- * Baggage;
    name;
    int age;
}

class Plane {
    // TODO cancel/modify FlightPlan
    1 -- * FlightPlan;
    // Aircraft Registration
    immutable registration;
    immutable int seats;
    immutable make;
    immutable model;
    immutable series;
    // Unscheduled general aviation will generally use registration
    // Commercial operators will use ICAO airline designator and route number
    flightCode = registration;
    // Defaults to flight code but may be reassigned by Air Traffic Control
    defaulted callsign = flightCode;
    // Gallons of fuel in the tanks
    // TODO fuel min / max guards
    // Cannot have negative fuel or exceed capacity
    // TODO define fuel capacity
    lazy double fuel;
    lazy boolean isEmergency;
    // TODO remove and use AirTrafficControl request / acknowledge clearance
    lazy boolean isCleared;
    // Weather deposits ice on the control surfaces
    lazy boolean hasIce;
    // TODO altitude should initialize to altitude of current airport
    int altitude;
    // A crude function to print the entry, action, and exit state transitions
    private void printTransition(String event, String state) {
        System.out.println("Flight " + getCallsign() + " " + event + " " + state);
    }
    // TODO handle emergencies (flight, passenger, etc)
    // Add declareEmergency() function and appropriate transition to all states.
    // Add [isEmergency] guarded auto transition to all state to handle emergency
    // declared in any earlier state.
    Flight {
        Standby {
            entry / {
                printTransition("entering", "Standby");
            }
            // TODO: add has FlightPlan *today* Guard
            preFlightCheck() [hasFlightPlans()] -> PreFlight;
        }
        PreFlight {
            entry / {
                printTransition("entering", "PreFlight");
            }
            // TODO Guard AirTrafficControl approve (check isValid) FlightPlan
            isFlightReady() -> Board;
        }
        Board {
            entry / {
                printTransition("entering", "Board");
            }
            [isEmergency] -> Disembark;
            // TODO AirTrafficControl.requestClearance(this);
            [!isEmergency] -> Ready;
        }
        FlightPrep {
            entry / {
                printTransition("entering", "FlightPrep");
            }
            -> TakeoffPipeline;
        }
        TakeoffPipeline {
            entry / {
                printTransition("entering", "TakeoffPipeline");
            }
            Ready {
                entry / {
                    printTransition("entering", "Ready");
                }
                [hasIce] -> DeIce;
            }
            DeIce {
                entry / {
                    printTransition("entering", "DeIce");
                }
                // Aircraft must takeoff within the deicing fluid holdover or else wait
                // to be deiced again
                //
                // TODO the holdover period should be a constant defined by the
                // AviationControlSystem. Magic numbers buried in the code are bad practice.
                // 15 minutes
                after(15*60) [hasIce] -> DeIce;
            }
            // Before a flight can land or takeoff, an explicit permission must be issued
            // that expires after a set amount of time
            //
            // TODO get timeout from local AirTrafficControl
            // 60 minutes (arbitrary)
            after(60*60) [!isCleared] / {
                printTransition("transition", "TakeoffPipeline -> TakeoffPrep");
            } -> FlightPrep;
            // TODO replace with AirTrafficControl.acknowledgeClearance(Plane);
            // 'rotate' is jargon for when an aircraft lifts up from the runway
            rotate() / {
                printTransition("transition", "TakeoffPipeline -> Transit");
            } -> Transit;
        }
        Transit {
            entry / {
                printTransition("entering", "Transit");
            }
            // TODO
            // Add transitions between AirTrafficControllers en route as the aircraft
            // enters/leaves their airspace
            -> LandingPipeline;
        }
        LandingPipeline {
            entry / {
                printTransition("entering", "LandingPipeline");
            }
            land() [isCleared] -> Disembark;
        }
        // TODO handle Missing In Action / Crash states
        Disembark {
            entry / {
                // Any landing you walk away from is a good landing
                printTransition("entering", "Disembark");
            }
            -> Standby;
            exit / {
                // Remove the completed FlightPlan from the queue
                // TODO: use the sorted directive to make sure flights are sorted by Date
                removeFlightPlan(getFlightPlan(0));                
            }
        }
    }
}

// TODO model after umple Runway state machine example 
class Runway {
    * -> * GroundCrew;
    int capacity;
    //int id;
    boolean isLatitudinal;
    status {
        Open {
            Free {
                -> Occupied;
            }
            Occupied {
            }
        }
        Closed {
            Emergency {
                -> ReadyToOpen;
            }
            Maintenance {
                -> ReadyToOpen;
            }
            ReadyToOpen {
            }
        }
    }
    direction {
        Undefined {
            [isLatitudinal] -> North;
            [!isLatitudinal] -> East;
        }
        North {
            changeDirection() -> South;
        }
        South {
            changeDirection() -> North;
        }
        East {
            changeDirection() -> West;
        }
        West {
            changeDirection() -> East;
        }
    }
}
